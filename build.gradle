import org.gradle.internal.os.OperatingSystem

plugins {
	id "eclipse"
	id "idea"
	id "java"
	id "java-library"
	id "application"
}

sourceCompatibility = "8"
mainClassName = "ide.IDE"
project.ext.lwjglVersion = "3.2.3"
project.ext.jomlVersion = "1.9.19"
project.ext.gdxVersion = "1.9.3"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		def osArch = System.getProperty("os.arch")
		project.ext.lwjglNatives = osArch.startsWith("arm") || osArch.startsWith("aarch64")
			? "natives-linux-${osArch.contains("64") || osArch.startsWith("armv8") ? "arm64" : "arm32"}"
			: "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = System.getProperty("os.arch").contains("64") ? "natives-windows" : "natives-windows-x86"
		break
}

sourceSets {
	main {
		java.srcDir("${projectDir}/src/main/java/")
		resources.srcDir("${projectDir}/src/main/resources/")
	}

	test {
		java.srcDir("${projectDir}/src/test/java/")
		resources.srcDir("${projectDir}/src/test/resources/")
	}	
}

repositories {
	mavenCentral()
}

dependencies {
	api "com.badlogicgames.gdx:gdx-bullet:$gdxVersion"
	runtimeOnly "com.badlogicgames.gdx:gdx-bullet-platform:$gdxVersion:natives-desktop"
	api "com.esotericsoftware:kryonet:2.22.0-RC1"
	api "com.googlecode.json-simple:json-simple:1.1.1"
	api "org.luaj:luaj-jse:3.0.1"
	api files('libs/lwjgui-1.0.0-SNAPSHOT.jar')

	// LWJGL dependencies
	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

	api "org.lwjgl:lwjgl"
	api "org.lwjgl:lwjgl-assimp"
	api "org.lwjgl:lwjgl-glfw"
	api "org.lwjgl:lwjgl-jemalloc"
	api "org.lwjgl:lwjgl-nanovg"
	api "org.lwjgl:lwjgl-nfd"
	api "org.lwjgl:lwjgl-openal"
	api "org.lwjgl:lwjgl-opengl"
	api "org.lwjgl:lwjgl-stb"
	api "org.lwjgl:lwjgl-tinyfd"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-jemalloc::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-nanovg::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-nfd::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-tinyfd::$lwjglNatives"

	api "org.joml:joml:${jomlVersion}"
}
